?expression: rename 
           | create
           | obsolete 
           | delete 
           | move
           | create_class 
           | unobsolete
           | deepen
           | shallow
           | change_relationship
           | create_edge
           | delete_edge 
           | create_synonym 
           | remove_from_subset
           | add_to_subset 
           | create_annotated_edge
           | delete_annotated_edge
           | add_subsumption_axiom
           | add_existential_restriction_axiom
           | delete_subsumption_axiom
           | delete_existential_restriction_axiom

rename : "rename" [id "from"] old_label [old_language] ("to"|"as"|"->") new_label [new_language]

create : "create node" id label [language]
create_class: "create" id

create_synonym: "create" [synonym_qualifier] "synonym" synonym [language] "for" id 
synonym_qualifier : /exact|narrow|broad|related/

delete : "delete" entity

obsolete : "obsolete" entity
         | "obsolete" entity "with replacement" replacement

unobsolete: "unobsolete" id 

deepen : "deepen" id "from" old_id ("to"|"->") new_id
shallow : "shallow" id "from" old_id ("to"|"->") new_id 
move : "move" subject predicate object "from" old_id ("to"|"as"|"->") new_id 

create_edge : "create edge" subject predicate object
delete_edge : "delete edge" subject predicate object

create_annotated_edge : "create edge <<" subject predicate object ">>" annotation_property annotation
delete_annotated_edge : "delete edge <<" subject predicate object ">>" annotation_property annotation

change_relationship: "change relationship between" subject "and" object "from" old_predicate "to" new_predicate 

add_subsumption_axiom: "add" subclass "SubClassOf" superclass
add_existential_restriction_axiom: "add" subclass "SubClassOf" property "some" filler

delete_subsumption_axiom: "delete" subclass "SubClassOf" superclass
delete_existential_restriction_axiom: "delete" subclass "SubClassOf" property "some" filler

remove_from_subset :  "remove" id "from subset" subset
add_to_subset :  "add" id "to subset" subset

replacement : ID
id : ID 
subject: ID
predicate: ID
object: TERM

subset: ID
synonym : LABEL

subclass: ID
superclass: ID
property: ID
filler: ID

label : SINGLE_QUOTE_LITERAL
old_label : SINGLE_QUOTE_LITERAL
new_label : SINGLE_QUOTE_LITERAL

language: LANGUAGE_TAG
old_language : LANGUAGE_TAG
new_language : LANGUAGE_TAG
LANGUAGE_TAG :  /[a-zA-Z]+(?:-[a-zA-Z0-9]+)*/

annotation_property : STRING
annotation : STRING

old_id : ID
new_id : ID

old_predicate : ID
new_predicate : ID

entity: ID
        | LABEL

LABEL: SINGLE_QUOTE_LITERAL

TERM : ID
       | TRIPLE_SINGLE_QUOTE_LITERAL
       | TRIPLE_DOUBLE_QUOTE_LITERAL
       | SINGLE_QUOTE_LITERAL
       | DOUBLE_QUOTE_LITERAL

DOUBLE_QUOTE_LITERAL : "\"" INNER_DOUBLE_QUOTE_LITERAL "\""
TRIPLE_DOUBLE_QUOTE_LITERAL : "\"\"\"" INNER_TRIPLE_DOUBLE_QUOTE_LITERAL "\"\"\""

TRIPLE_SINGLE_QUOTE_LITERAL : "'''" INNER_TRIPLE_SINGLE_QUOTE_LITERAL "'''" 
SINGLE_QUOTE_LITERAL : "'" INNER_SINGLE_QUOTE_LITERAL "'" 

INNER_DOUBLE_QUOTE_LITERAL: /((?!\").)*/
INNER_TRIPLE_DOUBLE_QUOTE_LITERAL: /((?!\"\"\").)*/

INNER_SINGLE_QUOTE_LITERAL: /((?!\').)*/
INNER_TRIPLE_SINGLE_QUOTE_LITERAL: /((?!\'\'\').)*/

ID :  "<" INNER_ID ">" 

INNER_ID: /\S+/
STRING: /\S+/

%import common.WS
%ignore WS
