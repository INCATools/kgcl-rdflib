?expression: rename 
           | create
           | obsolete 
           | delete 
           | move
           | create_class 
           | unobsolete
           | deepen
           | shallow
           | change_relationship
           | create_edge
           | delete_edge 
           | create_synonym 
           | remove_from_subset
           | add_to_subset 
           | create_annotated_edge
           | delete_annotated_edge
           | add_subsumption_axiom
           | add_existential_restriction_axiom
           | delete_subsumption_axiom
           | delete_existential_restriction_axiom

rename : "rename" _WS [id _WS "from" _WS] old_label ["@" old_language] _WS ("to"|"as"|"->") _WS new_label ["@" new_language]

create : "create node" _WS id _WS label ["@" language]
create_class: "create" _WS id

create_synonym: "create" _WS [synonym_qualifier _WS] "synonym" _WS synonym ["@" language] _WS "for" _WS id 
synonym_qualifier : /exact|narrow|broad|related/

delete : "delete" _WS entity

obsolete : "obsolete" _WS entity
         | "obsolete" _WS entity _WS "with replacement" _WS replacement

unobsolete: "unobsolete" _WS id 

deepen : "deepen" _WS id _WS "from" _WS old_id _WS ("to"|"->") _WS new_id 
shallow : "shallow" _WS id _WS "from" _WS old_id _WS ("to"|"->") _WS new_id 

move : "move" _WS subject _WS predicate _WS object _WS "from" _WS old_id _WS ("to"|"as"|"->") _WS new_id 

create_edge : "create edge" _WS subject _WS predicate _WS object ["@" language | datatype]
delete_edge : "delete edge" _WS subject _WS predicate _WS object ["@" language | datatype]

create_annotated_edge : "create edge <<" subject _WS predicate _WS object ">>" _WS annotation_property _WS annotation
delete_annotated_edge : "delete edge <<" subject _WS predicate _WS object ">>" _WS annotation_property _WS annotation

change_relationship: "change relationship between" _WS subject _WS "and" _WS object _WS "from" _WS old_predicate _WS "to" _WS new_predicate 

add_subsumption_axiom: "add" _WS subclass _WS "SubClassOf" _WS superclass
add_existential_restriction_axiom: "add" _WS subclass _WS "SubClassOf" _WS property _WS "some" _WS filler

delete_subsumption_axiom: "delete" _WS subclass _WS "SubClassOf" _WS superclass
delete_existential_restriction_axiom: "delete" _WS subclass _WS "SubClassOf" _WS property _WS "some" _WS filler

remove_from_subset :  "remove" _WS id _WS "from subset" _WS subset
add_to_subset :  "add" _WS id _WS "to subset" _WS subset

replacement : ID
id : ID 
subject: ID
predicate: ID
object: TERM

subset: ID
synonym : LABEL

subclass: ID
superclass: ID
property: ID
filler: ID

label : SINGLE_QUOTE_LITERAL
old_label : SINGLE_QUOTE_LITERAL
new_label : SINGLE_QUOTE_LITERAL

language: LANGUAGE_TAG
old_language : LANGUAGE_TAG
new_language : LANGUAGE_TAG
datatype: "^^" STRING

LANGUAGE_TAG :  /[a-zA-Z]+(?:-[a-zA-Z0-9]+)*/

annotation_property : STRING
annotation : STRING

old_id : ID
new_id : ID

old_predicate : ID
new_predicate : ID

entity: ID
        | LABEL

LABEL: SINGLE_QUOTE_LITERAL

TERM : ID
       | TRIPLE_SINGLE_QUOTE_LITERAL
       | TRIPLE_DOUBLE_QUOTE_LITERAL
       | SINGLE_QUOTE_LITERAL
       | DOUBLE_QUOTE_LITERAL

DOUBLE_QUOTE_LITERAL : "\"" INNER_DOUBLE_QUOTE_LITERAL "\""
TRIPLE_DOUBLE_QUOTE_LITERAL : "\"\"\"" INNER_TRIPLE_DOUBLE_QUOTE_LITERAL "\"\"\""

TRIPLE_SINGLE_QUOTE_LITERAL : "'''" INNER_TRIPLE_SINGLE_QUOTE_LITERAL "'''" 
SINGLE_QUOTE_LITERAL : "'" INNER_SINGLE_QUOTE_LITERAL "'" 

INNER_DOUBLE_QUOTE_LITERAL: /((?!\").)*/
INNER_TRIPLE_DOUBLE_QUOTE_LITERAL: /((?!\"\"\").)*/

INNER_SINGLE_QUOTE_LITERAL: /((?!\').)*/
INNER_TRIPLE_SINGLE_QUOTE_LITERAL: /((?!\'\'\').)*/

ID :  "<" INNER_ID ">" 

INNER_ID: /((?!>).)*/
STRING: /\S+/

_WS: /[ \t\f\r\n]/+

